import "tfconfig/v2" as tfconfig

# find all managed resources that support tagging
resources = filter tfconfig.resources as _, resources {
	resources.mode is "managed" and
		resources.config.tags is defined and
		resources.config.tags.references is defined
}

# empty list to hold any violating resources
violations = []

# loop through each resource and check if it has a tag referencing a secret
for resources as name, resource {
	added = false
	for resource.config.tags.references as ref {
		if added == false {
			if ref matches "aws_secretsmanager_secret_version" {
				append(violations, name)
				added = true
			}
		}
	}
}

# list violating resources
if length(violations) > 0 {
	print("The following resources have a tag referencing a secret:")
	for violations as name {
		print(" - " + name)
	}
}

main = rule {
	length(violations) == 0
}
